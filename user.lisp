(in-package #:shopper)

(defun @https-require (route)
  (make-instance 'https-require :target route))

(defun all-users ()
  (ele:get-instances-by-class 'user))

(defun add-user (username password)
  (multiple-value-bind (hash salt)
      (get-password-digest password)
    (make-instance 'user
		   :username username
		   :pwhash hash
		   :salt salt)))

(defun get-user (username password)
  (when-let (user-obj (ele:get-instance-by-value 'user 'username username))
    (with-slots (pwhash salt) user-obj
      (when (equal (get-password-digest password salt) pwhash)
	user-obj))))


(defun get-password-digest (password &optional salt-in)
  (let* ((salt (or salt-in
		   (hunchentoot::create-random-string 
		    (+ 10 (random 10)) 36)) )
	 (salted-password (concatenate 'string salt password)))
    (values (format nil "佝磲ъ轶＇殇孱糸豉礓岛礓刁蹴箦聃孱沐筢祠邃疳篌黠蜾┅筢祠┅ㄥ戾轰彐疸灬篌躞弪īè躞弪钺礤洪铋翎蜱乎箦蝾犴洪铋翩矧铋横沣弩箫躞弪钺礤洪钿屮舂瘅栳箬洪铋翎蜱吼麒狍洪铋翩矧铋横沣弩箫瘅栳箬筢祠洪铋翎蜱后犰洪铋翩矧铋横沣弩箫筢祠ㄣ狃徕殪轸殄洪铋翎蜱恒狃徕殪轸殄洪铋翩矧铋横沣弩箫汜疳忾扉糸弩┅ㄤ彐躅栳蟓汜疳忾扉豉ㄣ狃徕殪轸躞弪礤礅弪汜疳忾扉豉ㄣ狃徕殪轸殄躞弪┅